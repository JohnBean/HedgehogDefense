;libCombatModel
;  Variables and procedures specific to the combat model

globals [
  ; Variables used in the calculation of the combat model
  cm_coeffs
  cm_attritionScalar
]

directed-link-breed [ directFiring directFire ]
directed-link-breed [ indirectFiring indirectFire ]

to setup-CombatModel
  ;Turkes linear Lanchester numbers, derived from Battle of Kursk
  ;  Soviet (aka French) and German, respectively
  ;  set cm_coeffs [ .0000000268 .0000000668 ]
  set cm_coeffs [ .00000268 .00000668 ]     ; scaled up, FIXME justify

  ; uses new state system: s_P_DEFENSE, s_DEFENSE, s_ATTACK, s_B_ATTACK, s_RETREAT, s_ROUTE, s_RESERVE, s_Q_BRIDGE, s_ON_BRIDGE, s_OVR_BRIDGE
  set cm_attritionScalar [ 0.5          0.8        1         1.2         1          1.5      1          0.8         1.5          1 ]
end

;This turtle marks the nearest enemy as either the direct or indirect (out of direct range) target it wants to engage
to cm_declareTarget
  let opponent c_nearestEnemy
  if (opponent = nobody) [stop]
  
  ifelse (c_distance opponent <= curDRange) [
    create-directFire-to opponent [set hidden? not fireAnimation]
  ] [
    if (c_distance opponent <= curIRange) [ create-indirectFire-to opponent [set hidden? not fireAnimation] ]
  ]
end

;This turtle attrits its target (if any), as well as any turtles directly attacking it
;  NOTE: this should only be called after all turtles have declared their targets
;  IMPLEMENTATION: this adds a portion of this turtle's strength to each opponent's enemyStrength variable
to cm_attritTargets
  let indirectOnly false
  
  ;Set opponents to be my direct target + anyone directly attacking me
  let opponents (turtle-set in-directFire-neighbors out-directFire-neighbors)

  ;If no direct targets and no one directly attacking me,
  ;  then set indirectOnly flag and my opponent to be my indirect target
  if (count opponents = 0) [
    set indirectOnly true
    set opponents out-indirectFire-neighbors
  ]
  
  ;If no targets at all, no need to calculate attrition
  if (count opponents = 0) [stop]
  
  ;
  ifelse (not indirectOnly) [
    ;Calculate my and the total enemy strengths
    let attriterStrength cm_reportDirStrength self
    let totalOppStrength 0
    ask opponents [ set totalOppStrength (totalOppStrength + cm_reportDirStrength self) ]
    
    ;Apportion the attrition I can deal, across all opponents, proportionally to each according to their strength
    ask opponents [
      ;this opponent's strength as a proportion of the total
      let toThisOpponent (cm_reportDirStrength self / totalOppStrength)

      set enemyStrength (enemyStrength + (attriterStrength * toThisOpponent))
    ]    
  ] [
    if (count opponents > 1) [error "libCombatModel: indirectOnly = true, but I have more than one opponent"]        ;DEBUG

    ;Only use my artillery strength
    let attriterStrength cm_reportIndStrength self    
    ask opponents [set enemyStrength (enemyStrength + attriterStrength)]
  ]
end

;This turtle applies attrition to itself
;  IMPLEMENTATION: updates its effectiveness, using its current enemyStrength value
to cm_realizeAttrition
  let oppCoeff 0
  ifelse (allegiance = GERMAN) [
    set oppCoeff (item FRENCH cm_coeffs) 
  ] [
    set oppCoeff (item GERMAN cm_coeffs)
  ]
  set oppCoeff (oppCoeff * (item newState cm_attritionScalar))   ; scale the attrition being done to me by my current state
  let myStrength cm_reportDirStrength self
  
  ;Lanchester Linear model
  let attrition (1 - oppCoeff * enemyStrength)
  cm_allocateAttrition attrition
  
  ;Update effectiveness & force retreat state if appropriate
  set effectiveness (cm_reportDirStrength self / cm_reportMaxStrength self)
  if (effectiveness < 0.05) [            ;Lanchester needs to be zeroed out for arbitrarily small units
    set effectiveness 0
    set color gray
    set size 4
  ]
  if (newState != sn_RETREAT) [
    let forceRetreat nobody
    ifelse (allegiance = FRENCH) [
      if (effectiveness < beginEffectiveness - FrenchForceRetreat) [ set forceRetreat FrenchForceRetreat ]
    ] [
    if (effectiveness < beginEffectiveness - GermanForceRetreat) [ set forceRetreat GermanForceRetreat ]
    ]
    if ( forceRetreat != nobody ) [
      set beginState newState
      if ( beginState = sn_P_DEFENSE ) [ set beginState sn_DEFENSE ]    ; as unit can't prepare a defensive position quickly enough
      set newState sn_RETREAT
      set isNewState? true
    ]
  ]
  
  ;Reset the strength directed towards me
  set enemyStrength 0
end

;----------------------------------------------
;==          UTILITY FUNCTIONS               ==
;----------------------------------------------
to-report cm_reportDirStrength [ unit ]
  report ([curInf] of unit) + (10 * [curAT] of unit) + (20 * [curTanks] of unit) + (40 * [curArt] of unit)
end

to-report cm_reportIndStrength [ unit ]
  report (40 * [curArt] of unit)
end

to-report cm_reportMaxStrength [ unit ]
  report ([startInf] of unit) + (10 * [startAT] of unit) + (20 * [startTanks] of unit) + (40 * [startArt] of unit)
end

;Proportionally allocates this turtle's attrition to the various combat systems (deterministic)
;  NOTE: input is attrition as a percentage (that is, newStrength / oldStrength)
;  IMPLEMENTATION: also updates the turtle's effectiveness value and sets triggers for Phil's code
to cm_allocateAttrition [ attrition ]
  set curInf (curInf * attrition)
  set curAT (curAT * attrition)
  set curTanks (curTanks * attrition)
  set curArt (curArt * attrition)
end