;libCommon
;  Common variables used by all simulations

globals [
  TimeScale       ;how many hours (or portions of one) in a tick
  
  ;General Macros
  FRENCH GERMAN
  s_RETREAT    
  i_INF i_TANK i_AT i_ART
  INITIAL_FRENCH_HEADING
  
  ;Tables of historical values
  SystemRanges
  
  ;Utility variables
  DefaultUnit
]

breed [units unit]

units-own [
  name            ;the unit's historical name
  superior        ;the historical larger formation the unit is attached to
  allegiance      ;which side the units is part of (FRENCH = 0, GERMAN = 1)

  ;Unit's starting statistics
  startInf
  startTanks
  startAT
  startArt
  maxSpeed        ;in km per hour
  maxRange        ;in km

  ;Unit's current statistics
  curInf
  curTanks
  curAT
  curArt
  curSpeed
  curRange
  
  ;Combat variables
  engaged         ;a unit may be engaged only if engaged=0 (i.e. strictly two-party combat)
  effectiveness   ;unit's remaining strength, out of 100(%)
  
  ;Bridge variables
  targetBridge    ;0 none / already crossed, 1 abbeville bridge, 2 amiens bridge, 3 perrone bridge
  bridgeCrossed
  destinationNum
  destinationX
  destinationY
  
  state           ;1 ready for orders, 2 moving to position(nazi)/retreat (french), 3 crossing bridge, 4 ready for orders after crosssed, 5 moving after crossed, 6 full retreat
  retreatState    ;1 for retreating to the line of 70% remaining effectiveness, 2 for 50%, 3 for 30%
  stepsTaken      ;number of ticks a given french brigade has spent retreating
]

;Main library setup, should always be called once at the beginning
to setup-Common
  __clear-all-and-reset-ticks  ;clear the screen
  set TimeScale 1
  
  ;General index macros for 2D lists
  set FRENCH 0
  set GERMAN 1
  set s_RETREAT 6
  set i_INF 0
  set i_TANK 1
  set i_AT 2
  set i_ART 3
  set INITIAL_FRENCH_HEADING 45
  
  ;Contains effective ranges (in meters) for the system types
  ;  Note: these are average effective ranges to hit - penetration et al. are included in Lanchester constants
  ;  (Infantry, Tanks, AT, Artillery)
  set SystemRanges [ [350 500 700 8500]
                     [400 550 700 8500] ]
  
  ;Default (scratch) unit statistics
  ;  (name, superior, allegiance, startInf, startTanks, startAT, startArt, maxSpeed, maxRange)
  set DefaultUnit [ "X Composite Brigade" "Y Composite Division" 99 3500 80 90 12 0.8125 25]
end

to c_writeUnit [ statsList ]
  set name (item 0 statsList)
  set superior (item 1 statsList)
  set allegiance (item 2 statsList)
  set startInf (item 3 statsList)
  set startTanks (item 4 statsList)
  set startAT (item 5 statsList)
  set startArt (item 6 statsList)
  set maxSpeed (item 7 statsList)
  set maxRange (item 8 statsList)
  
  set curInf startInf
  set curTanks startTanks
  set curAT startAT
  set curArt startArt
  set curSpeed maxSpeed
  set curRange maxRange
  
  set shape "Default"
  set size 7
  set engaged 0
  set effectiveness 100
end

to-report c_nearestEnemy
  let otherSide -1
  ifelse (allegiance = GERMAN) [
    set otherSide FRENCH
  ] [
    set otherSide GERMAN
  ]
  report min-one-of units with [allegiance = otherSide] [distance myself]
end

to-report c_nearestUnengagedEnemy
  let otherSide -1
  ifelse (allegiance = GERMAN) [
    set otherSide FRENCH
  ] [
    set otherSide GERMAN
  ]
  report min-one-of units with [allegiance = otherSide and engaged = 0] [distance myself]
end

to c_clearEngaged
  ask units [
    set engaged 0
  ]
end