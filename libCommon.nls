;libCommon
;  Common variables used by all simulations

globals [
  ;General Index Macros
  FRENCH GERMAN    
  i_INF i_TANK i_AT i_ART
  
  ;State Macros
  s_P_DEFENSE s_DEFENSE s_ATTACK s_B_ATTACK s_RETREAT s_ROUTE s_RESERVE s_Q_BRIDGE s_ON_BRIDGE s_OVR_BRIDGE

  ;Simulation Variables
; TimeScale       ;How many hours (or portions of one) in a tick
  MapScale        ;The conversion factor between sim distance -> map distance (aka sim_distance * MapScale = map_distance)
  
  ;Historical values
  SystemRanges    ;Contains effective ranges (in km) for the system types
    
  ;Utility variables
  GermanInfBrigade GermanMotBrigade GermanPzrBrigade
  FrenchInfBrigade FrenchCavBrigade FrenchDLMBrigade FrenchDCrBrigade
]

breed [units unit]

units-own [
  name            ;the unit's historical name
  superior        ;the historical larger formation the unit is attached to
  allegiance      ;which side the units is part of (FRENCH = 0, GERMAN = 1)

  ;Unit's starting statistics
  startInf
  startTanks
  startAT
  startArt
  maxSpeed        ;in km per hour

  ;Unit's current statistics
  curInf
  curTanks
  curAT
  curArt
  curSpeed
  curDRange       ;base range of direct fire weapons, in km
  curIRange       ;base range of indirect fire weapons, in km
  effectiveness   ;unit's remaining strength, as a percentage (1.0 -> 0.0)
  
  ;Variables for unit behavior
  state               ;unit's current behavior
                      ;  s_P_DEFENSE, s_DEFENSE, s_ATTACK, s_B_ATTACK, s_RETREAT, s_ROUTE, s_RESERVE, s_Q_BRIDGE, s_ON_BRIDGE, s_OVR_BRIDGE
  beginEffectiveness  ;unit's effectiveness when starting current elected behavior
  beginState          ;unit's state before being switched
  beginHeading        ;unit's declared heading when starting current elected behavior
  beginRow            ;unit's starting row
  targetPatch
  isNewState?         ;whether or not the current state is newly set
  
  ;Combat variables
  enemyStrength   ;total enemy strength directed at this unit at the end of the tick
                  ;(e.g. if dX = -Byx, then this variable will be -(y1 + y2 + y3 + ... + yn)
                  ;  and dX = -(enemyStrength)Bx
  
  ;Bridge variables
  targetBridge    ;0 none / already crossed, 1 abbeville bridge, 2 amiens bridge, 3 perrone bridge
  bridgeCrossed
  destinationNum
  destinationX
  destinationY
]

;Main library setup, should always be called once at the beginning
to setup-Common
  __clear-all-and-reset-ticks
  
  ;General index macros for 2D lists
  set FRENCH 0
  set GERMAN 1
  set i_INF 0
  set i_TANK 1
  set i_AT 2
  set i_ART 3

  ;State macros
  set s_P_DEFENSE 0
  set s_DEFENSE 1
  set s_ATTACK 2
  set s_B_ATTACK 3
  set s_RETREAT 4
  set s_ROUTE 5
  set s_RESERVE 6
  set s_Q_BRIDGE 7
  set s_ON_BRIDGE 8
  set s_OVR_BRIDGE 9
  
  ;Simulation variables
  set MapScale ( 125 / world-height)  ; For Google Maps "map.png", the map covers 125km vertically, and is square
  
  ;Historical values
  ;Note: these are average effective ranges to hit - penetration et al. are included in Lanchester constants
  ;  (FRENCH, GERMAN)
  ;  (Infantry, Tanks, AT, Artillery)
  set SystemRanges [ [0.35 0.5  0.7 9]
                     [0.4  0.55 0.7 9] ]
  let FrenchRanges (item FRENCH SystemRanges)
  let GermanRanges (item GERMAN SystemRanges)
  
  ;Divide a division into 4 equal brigades (including their portion of divisional assets)
  ;  counting armored cars as 1/4 of a tank
  ;  30 infantry divisions + 6 in reserve
  set GermanInfBrigade (list "X Inf Brigade" "X Inf Division" GERMAN 4125 0.1875 18.75 18.25 5 (item i_INF GermanRanges) (item i_ART GermanRanges))
  ;  6 motorized divisions
  set GermanMotBrigade (list "Y Mot Brigade" "Y Mot Division" GERMAN 4125 1.875 18.75 18.25 30 (item i_INF GermanRanges) (item i_ART GermanRanges))
  ;  6 panzer Divisons
  set GermanPzrBrigade (list "Z Pzr Brigade" "Z Pzr Division" GERMAN 2950 72.5 12.5 9.5 30 (item i_INF GermanRanges) (item i_ART GermanRanges))
  
  ;  13 infantry divisions + 7 in reserve
  set FrenchInfBrigade (list "A Inf Brigade" "A Inf Division" FRENCH 2318 5 12 15 5 (item i_INF FrenchRanges) (item i_ART FrenchRanges))
  ;  3 cavalry divisions (heavily weakened)
  set FrenchCavBrigade (list "B Cav Brigade" "B Cav Division" FRENCH 875 5 6 6 15 (item i_INF FrenchRanges) (item i_ART FrenchRanges))
  ;  3 armored light divisions (reconstructed, all < full strength)
  set FrenchDLMBrigade (list "C DLM Brigade" "C DLM Division" FRENCH 1950 6 6 6 15 (item i_INF FrenchRanges) (item i_ART FrenchRanges))
  ;  3 armored divisions (reconstructed, all < 1/2 strength)
  set FrenchDCrBrigade (list "D DCr Brigade" "D DCr Division" FRENCH 1221 18 21 11 25 (item i_INF FrenchRanges) (item i_ART FrenchRanges))
end

;----------------------------------------------
;==          UTILITY FUNCTIONS               ==
;----------------------------------------------
to c_writeUnit [ statsList ]
  set name (item 0 statsList)
  set superior (item 1 statsList)
  set allegiance (item 2 statsList)
  set startInf (item 3 statsList)
  set startTanks (item 4 statsList)
  set startAT (item 5 statsList)
  set startArt (item 6 statsList)
  set maxSpeed (item 7 statsList) / MapScale * TimeScale       ;converted to simulation distance per tick
  ;fixme FIX ME
  set curInf startInf
  set curTanks startTanks
  set curAT startAT
  set curArt startArt
  set curSpeed maxSpeed
  set curDRange (item 8 statsList) / MapScale                  ;converted to simulation distance
  set curIRange (item 9 statsList) / MapScale                  ;converted to simulation distance
  
  set shape "Default"
  set size 7
  set effectiveness 1
  set beginEffectiveness effectiveness
  set isNewState? false
end

to c_move [ moveDistance ]
  ifelse MoveAnimation [
    forward moveDistance
  ] [
    jump moveDistance
  ]
end

to-report c_nearestEnemy
  let otherSide -1
  ifelse (allegiance = GERMAN) [ set otherSide FRENCH ]
  [ set otherSide GERMAN ]
  report min-one-of units with [allegiance = otherSide and effectiveness > 0] [distance myself]
end

to-report c_nearestAvailEnemy
  let otherSide -1
  ifelse (allegiance = GERMAN) [ set otherSide FRENCH ]
  [ set otherSide GERMAN ]
  report min-one-of units with [allegiance = otherSide and effectiveness > 0 and count in-directFire-neighbors < 4] [distance myself]  
end